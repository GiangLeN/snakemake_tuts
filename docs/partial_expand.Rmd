---
title: "Snakemake expand tutorial"
author: "Giang Le"
date: "`r format(Sys.Date(), '%d-%m-%Y')`"
version: 0.1.2
output:
   html_document:
      toc: true # table of content true
      toc_float: true
      toc_depth: 3  # up to three depths of headings (specified by #, ## and ###)
      fig_caption: true
      highlight: tango  # specifies the syntax highlighting style
      number_sections: true  ## if you want number sections at each table header
      theme: spacelab
---

This tutorial is inspired by this question [here](https://stackoverflow.com/questions/40398091/how-to-do-a-partial-expand-in-snakemake).

## Expand for all combinations

Consider the normal usage of `expand()` function,

```
LETTERS = ["A", "B"]
NUMS = ["1", "2"]

rule all:
    input:
        expand("combined_{letter}.txt", letter=LETTERS)

rule generate_text:
    output:
        "text_{letter}_{num}.txt"
    shell:
        """
        echo "{wildcards.letter}" "{wildcards.num}" > {output}
        """

rule combine_text:
    input:
        expand("text_{letter}_{num}.txt", letter= LETTERS, num=NUMS)
    output:
        "combined_{letter}.txt"
    shell:
        """
        cat {input} > {output}
        """
```

We are asking *Snakemake* to create the input files: *text_A_1.txt*, *text_A_2.txt*, *text_B_1.txt* and *text_B_2.txt* using the rule `generate_text`.
After that we want to group the same ***letters*** with different ***numbers*** into the correspond file ***combined_{letter}.txt***.

Both files *combined_A.txt* and *combined_B.txt* are created.
However they have the same content.

```
cat combined_A.txt
```

```
A 1
A 2
B 1
B 2
```

```
cat combined_B.txt
```

```
A 1
A 2
B 1
B 2
```

In this case, the `expand()` function creates all the possible combinations from both lists.
For the `rule combine_text`, Snakemake ran the command below twice:

```
cat text_A_1.txt, text_A_2.txt, text_B_1.txt, text_B_2.txt > combined_{letter}.txt
```

## Partial expand

Our aim is still the same, which is group all files with similar ***letters*** together.

Update `rule combine_text` as follow:

```
rule combine_text:
    input:
        expand("text_{{letter}}_{num}.txt", num=NUMS)
    output:
        "combined_{letter}.txt"
    shell:
        """
        cat {input} > {output}
        """
```

```{r echo=FALSE}
xfun::embed_file('partial_expand_smk/partial_2lists.smk', text = 'Download snakefile')
```

Here we use double bracket for non-expanded wildcard.
With partial expand the `rule combine_text` became

```
cat text_{letter}_1.txt text_{letter}_2.txt > combined_{letter}.txt
```

The files *combined_A.txt* and *combine_B.txt* are no longer the same.

```
cat combined_A.txt
```

```
A 1
A 2
```

```
cat combined_B.txt
```

```
B 1
B 2
```

Another way to write `input` is to include `allow_missing=True`: 
```
expand("text_{letter}_{num}.txt", num=NUMS, allow_missing=True)
```
This will also produce the same result.


## Expand further 

Lets add the third variable ***samples***.
Our aim is to group the same ***samples*** and ***letters*** together.

```
LETTERS = ["A", "B"]
NUMS = ["1", "2"]
SAMPLES = ["S1","S2"]

rule all:
    input:
        expand("{sample}_{letter}.txt", sample = SAMPLES, letter=LETTERS)

rule generate_text:
    output:
        "{sample}_{letter}_{num}.txt"
    shell:
        """
        echo {wildcards.sample} {wildcards.letter} {wildcards.num} > {output}
        """

rule combine_text:
    input:
        expand("{{sample}}_{{letter}}_{num}.txt", num=NUMS)
    wildcard_constraints:
        letter="|".join(LETTERS)
    output:
        "{sample}_{letter}.txt"
    shell:
        """
        cat {input} > {output}
        """
```

```{r echo=FALSE}
xfun::embed_file('partial_expand_smk/partial_3lists.smk', text = 'Download snakefile')
```

The `wildcard_constraints` is added to specify that the letter can only come from the list LETTERS. 
Without the constraint, Snakemake will fail caused by *AmbigousRuleException*.


In this case ***numbers*** are grouped into ***sample*** and ***letter***.
Four final files are generated *S1_A.txt*, *S1_B.txt*, *S2_A.txt* and *S2_B.txt*.

```
cat S1_A.txt
```

```
S1 A 1
S2 A 2
```

```
cat S2_B.txt 
```

```
S2 B 1
S2 B 2
```

## Combine to samples (2 steps) 

Lets go one step further and generate a rule to combine all under ***samples***.
We need to update `wildcard_constraints` and move it to global.
This will impact all rules.

```
LETTERS = ["A", "B"]
NUMS = ["1", "2"]
SAMPLES = ["S1","S2"]

wildcard_constraints:
    letter="|".join(LETTERS),
    sample="|".join(SAMPLES)

rule all:
    input:
        expand("{sample}.txt", sample = SAMPLES)

rule generate_text:
    output:
        "{sample}_{letter}_{num}.txt"
    shell:
        """
        echo {wildcards.sample} {wildcards.letter} {wildcards.num} > {output}
        """

rule combine_text:
    input:
        expand("{{sample}}_{{letter}}_{num}.txt", num=NUMS)
    output:
        "{sample}_{letter}.txt"
    shell:
        """
        cat {input} > {output}
        """

rule combine_sample:
    input:
        expand("{{sample}}_{letter}.txt", letter=LETTERS)
    output:
        "{sample}.txt"
    shell:
        """
        cat {input} > {output}
        """
```

```{r echo=FALSE}
xfun::embed_file('partial_expand_smk/partial_3lists2steps.smk', text = 'Download snakefile')
```

The two generated files:

```
cat S1.txt
```

```
S1 A 1
S1 A 2
S1 B 1
S1 B 2
```

and 

```
cat S2.txt
```

```
S2 A 1
S2 A 2
S2 B 1
S2 B 2
```

The ***letters*** and ***numbers*** are grouped into the correct ***samples***.


## Combine to samples (1 step)

Same as above but in 1 step:

```
LETTERS = ["A", "B"]
NUMS = ["1", "2"]
SAMPLES = ["S1","S2"]

wildcard_constraints:
    sample="|".join(SAMPLES)

rule all:
    input:
        expand("{sample}.txt", sample = SAMPLES)

rule generate_text:
    output:
        "{sample}_{letter}_{num}.txt"
    shell:
        """
        echo {wildcards.sample} {wildcards.letter} {wildcards.num} > {output}
        """

rule combine_text:
    input:
        expand("{{sample}}_{letter}_{num}.txt", letter=LETTERS, num=NUMS)
    output:
        "{sample}.txt"
    shell:
        """
        cat {input} > {output}
        """
```

```{r echo=FALSE}
xfun::embed_file('partial_expand_smk/partial_3lists1step.smk', text = 'Download snakefile')
```

